plugins {
    id 'net.minecraftforge.gradleutils' version '2.+'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'de.jjohannes.extra-java-module-info' version '0.11'
}
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

group 'net.minecraftforge'

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

changelog {
    fromTag "1.0.0"
}

version = gradleutils.getTagOffsetVersion()
logger.lifecycle('Version: ' + version)

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

sourceSets {
    jmh
    testJars
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false

    automaticModule('typetools-0.8.3.jar', 'net.jodah.typetools')
    automaticModule('jmh-core-1.34.jar', 'jmh.core')
    automaticModule('powermock-core-2.0.9.jar', 'powermock.core')
    automaticModule('powermock-reflect-2.0.9.jar', 'powermock.reflect')
    automaticModule('jopt-simple-5.0.4.jar', 'jopt.simple')
}

ext.sharedManifest = manifest {
    attributes(['Specification-Title': 'eventbus',
                'Specification-Vendor': 'Forge',
                'Specification-Version': '1', // Currently version 1 of the eventbus specification
                'Implementation-Title': project.name,
                'Implementation-Version': "${project.version}+${System.getenv("BUILD_NUMBER")?:0}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
                'Implementation-Vendor' :'forge',
                'Implementation-Timestamp': java.time.Instant.now().toString(),
                'Git-Commit': gradleutils.gitInfo.abbreviatedId,
                'Git-Branch': gradleutils.gitInfo.branch,
                'Build-Number': "${System.getenv('BUILD_NUMBER')?:0}" ],
            'net/minecraftforge/eventbus/service/')
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}

task testsJar(type: Jar) {
    archiveClassifier = 'testsjar'
    from sourceSets.testJars.output
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


repositories {
    mavenLocal()
    maven {
        name = 'forge'
        url = 'https://maven.minecraftforge.net/'
    }
}

dependencies {
    implementation('org.ow2.asm:asm:9.1')
    implementation('org.ow2.asm:asm-commons:9.1')
    implementation('org.ow2.asm:asm-tree:9.1')
    implementation('org.apache.logging.log4j:log4j-api:2.17.2')
    implementation('cpw.mods:modlauncher:9.1.+')

    compileOnly('org.jetbrains:annotations:21.0.1')
    api('net.jodah:typetools:0.8.+')

    testImplementation(sourceSets.testJars.output)
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.+')
    testImplementation('org.powermock:powermock-core:2.0.+')
    testImplementation('cpw.mods:modlauncher:9.1.+')
    testImplementation('cpw.mods:securejarhandler:1.0.+')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.7.+')
    testImplementation('org.apache.logging.log4j:log4j-core:2.17.2')
    testImplementation('com.lmax:disruptor:3.4.2')
    testImplementation('org.jetbrains:annotations:21.0.1')

    testJarsImplementation(sourceSets.main.output)

    jmhImplementation(sourceSets.main.output)
    jmhImplementation(sourceSets.testJars.output)
    jmhImplementation('org.openjdk.jmh:jmh-core:1.34')
    jmhImplementation('org.powermock:powermock-core:2.0.+')
    jmhImplementation('cpw.mods:modlauncher:9.1.+')
    jmhImplementation('cpw.mods:securejarhandler:1.0.+')
    jmhAnnotationProcessor('org.openjdk.jmh:jmh-generator-annprocess:1.34')
}

configurations.jmhImplementation.extendsFrom(configurations.implementation)

test {
    dependsOn 'testsJar'
    useJUnitPlatform()

    systemProperty 'testJars.location', testsJar.archiveFile.get().asFile

    jvmArgs(
            '--module-path', classpath.asPath,
            '--add-modules', 'ALL-MODULE-PATH',
            '--add-opens', 'net.minecraftforge.eventbus/net.minecraftforge.eventbus=powermock.reflect',
            '--add-opens', 'net.minecraftforge.eventbus/net.minecraftforge.eventbus.api=powermock.reflect',
            '--add-opens', 'java.base/java.lang=powermock.reflect',
            '--add-opens', 'java.base/java.util.concurrent=powermock.reflect',
            '--add-reads', 'cpw.mods.modlauncher=net.minecraftforge.eventbus.test'
    )

    forkEvery 1
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
    mainClass.set('org.openjdk.jmh.Main')
    dependsOn 'testsJar'

    def results = file("${project.reportsDir}/jmh/result.json")
    doFirst {
        results.parentFile.mkdirs()

        systemProperty 'testJars.location', testsJar.archiveFile.get().asFile

        jvmArgs(
                '--module-path', sourceSets.jmh.runtimeClasspath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-opens', 'net.minecraftforge.eventbus.testjars/net.minecraftforge.eventbus.benchmarks.compiled=net.minecraftforge.eventbus.benchmarks',
                '--add-opens', 'java.base/java.lang=powermock.reflect',
                '--add-opens', 'java.base/java.util.concurrent=powermock.reflect',
                '--add-opens', 'cpw.mods.modlauncher/cpw.mods.modlauncher=powermock.reflect',
                '--add-reads', 'cpw.mods.modlauncher=net.minecraftforge.eventbus.benchmarks',
                '--add-exports', 'net.minecraftforge.eventbus.benchmarks/net.minecraftforge.eventbus.benchmarks.jmh_generated=jmh.core',
        )
    }

    args = [
            'net.minecraftforge.eventbus.benchmarks.EventBusBenchmark',
            '-bm', 'avgt',
            '-prof', 'stack',
            '-r', '5s',
            '-w', '5s',
            '-wi', '3',
            '-i', '3',
            '-f', '3',
            '-tu', 'ns',
            '-rf', 'json',
            '-rff', results
    ]
}

tasks['jmh'].dependsOn(clean)

artifacts {
    archives jar
    archives sourcesJar
    archives testsJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                name = 'Event Bus'
                description = 'High performance Event Bus library'
                url = 'https://github.com/MinecraftForge/eventbus'
                scm {
                    url = 'https://github.com/MinecraftForge/eventbus'
                    connection = 'scm:git:git://github.com/MinecraftForge/eventbus.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/eventbus.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/eventbus/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'LexManos'
                        name = 'LexManos'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}